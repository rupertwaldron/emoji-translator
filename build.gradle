plugins {
    id 'application'
    id 'com.dorongold.task-tree' version "2.1.1"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'jacoco'
}

group = 'com.ruppyrup'
version = '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}


application {
    mainClass = 'com.ruppyrup.Main'
}

tasks.register('runWithArg', JavaExec) {
    mainClass = 'com.ruppyrup.Main'
    classpath = sourceSets.main.runtimeClasspath
    args 'hello world'
}

tasks.register('runJar', JavaExec) {
    classpath shadowJar.outputs
//    classpath jar.outputs
//    classpath sourceSets.main.runtimeClasspath // addes libraries to jar
    mainClass = 'com.ruppyrup.Main'
    args 'uncle bob'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

ext {
    junitJupiterVersion = '5.9.1'
}


dependencies {
    implementation 'net.fellbaum:jemoji:1.1.5'
    testImplementation platform("org.junit:junit-bom:${junitJupiterVersion}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java {
            srcDir "$projectDir/src/integrationTest/java"
        }
        resources {
            srcDir "$projectDir/src/integrationTest/resources"
        }

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

tasks.register('integrationTest', Test) {
    group 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

jacoco {
    toolVersion = "0.8.9"
//    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jar {
    onlyIf {
        !tasks.named('shadowJar').isPresent() // only want the one jar if using shadowJar
    }
    manifest {
        attributes["Main-Class"] = "com.ruppyrup.Main"
    }
}

shadowJar {
    archivesBaseName = 'gradleDemo1'
    archiveVersion = '1.0-SNAPSHOT'
    manifest {
        attributes 'Main-Class': 'com.ruppyrup.Main'
    }
}


test.finalizedBy jacocoTestReport

wrapper {
    gradleVersion '8.1.1'
    setDistributionType Wrapper.DistributionType.ALL
}